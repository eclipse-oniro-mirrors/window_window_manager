# Copyright (c) 2021-2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/test.gni")
import("../../../../windowmanager_aafwk.gni")
module_out_path = "window_manager/extension/window_extension"

group("unittest") {
  testonly = true

  deps = [
    ":extension_window_extension_context_test",
    ":extension_window_extension_proxy_test",
    ":extension_window_extension_stub_impl_test",
  ]
}

ohos_unittest("extension_window_extension_proxy_test") {
  module_out_path = module_out_path

  sources = [ "window_extension_proxy_test.cpp" ]

  deps = [ ":window_extension_unittest_common" ]
}

ohos_unittest("extension_window_extension_stub_impl_test") {
  module_out_path = module_out_path

  sources = [ "window_extension_stub_impl_test.cpp" ]

  deps = [ ":window_extension_unittest_common" ]
}

ohos_unittest("extension_window_extension_context_test") {
  module_out_path = module_out_path

  sources = [ "window_extension_context_test.cpp" ]

  deps = [ ":window_extension_unittest_common" ]

  external_deps = [
    "ability_base:want",
    "ability_runtime:ability_context_native",
    "ability_runtime:ability_manager",
    "ability_runtime:ability_start_options",
    "ability_runtime:app_context",
    "ability_runtime:extensionkit_native",
    "ability_runtime:napi_common",
    "ability_runtime:runtime",
    "c_utils:utils",
    "common_event_service:cesfwk_innerkits",
    "eventhandler:libeventhandler",
    "graphic_2d:librender_service_client",
    "hilog:libhilog",
    "hitrace:hitrace_meter",
    "input:libmmi-client",
    "ipc:ipc_single",
    "napi:ace_napi",
  ]
}

## Build window_extension_unittest_common.a {{{
config("window_extension_unittest_common_public_config") {
  include_dirs = [
    "../../../../interfaces/innerkits/wm",
    "../../../../window_scene/session/host/include",
    "../../../../wm/include",
    "../../include",
    "../../include/zidl",
    "../../../extension_connection/include",
    "../../../extension_connection/include/zidl",
    "../../../../interfaces/innerkits/extension",
    "//third_party/googletest/googlemock/include",
  ]
}

ohos_static_library("window_extension_unittest_common") {
  visibility = [ ":*" ]
  testonly = true

  public_configs = [
    ":window_extension_unittest_common_public_config",
    "../../../../resources/config/build:coverage_flags",
    "../../../../resources/config/build:testcase_flags",
  ]

  public_deps = [
    "//commonlibrary/c_utils/base:utils",
    "//foundation/graphic/graphic_2d/rosen/modules/render_service_client:librender_service_client",
    "//foundation/multimedia/image_framework/interfaces/innerkits:image_native",
    "//foundation/multimodalinput/input/frameworks/proxy:libmmi-client",
    "//third_party/googletest:gmock",
    "//third_party/googletest:gtest_main",
  ]

  deps = [
    "../..:libwindow_extension",
    "../../../../interfaces/kits/napi/window_runtime:window_native_kit",
    "../../../../window_scene/session:scene_session",
    "../../../../wm:libwm",
    "../../../extension_connection:libwindow_extension_client",
  ]

  external_deps = [
    "c_utils:utils",
    "hilog:libhilog",
    "input:libmmi-client",
    "ipc:ipc_single",
  ]

  subsystem_name = "window"
  part_name = "window_manager"
}
